function 'EdwardCharles.NativeHTML.HTMLimage' = 
    (
        Width : DOUBLE, 
        Height : DOUBLE, 
        IsVisible : BOOLEAN, 
        HtmlContent : STRING, 
        CssContent : STRING 
    ) => 
    /// Setting default CSS for the HTML Block
    var DefaultCss = 
        "
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            display: flex;
            justify-content: center;
            padding: 50px;
            margin: 0;
        "

    var TotalCss = 
        IF( 
            CssContent = "", DefaultCss, CssContent
        )

    /// Sanitize/Encode Inputs
    /// 1. Replace <br> with <br/> and <hr> with <hr/> because SVG foreignObject requires strict XHTML.
    /// 2. Encode '#' to '%23' for CSS (Required for Data URLs).

    var SafeHtml = 
        SUBSTITUTE(
            SUBSTITUTE( HtmlContent, "<br>", "<br/>" ),
            "<hr>", "<hr/>"
        )

    var SafeCss = 
        SUBSTITUTE(
            SUBSTITUTE(TotalCss, "#", "%23" ),
            "'", "%27"
        )
    
    // Controls visibility of the HTML Block
    var VisibilityRule = IF( IsVisible, "", "visibility:hidden;" )
    
    /// Construct the SVG
    VAR SvgOutput = 
        "data:image/svg+xml;utf8," &
        "<svg xmlns='http://www.w3.org/2000/svg' width='" & Width & "' height='" & Height & "' viewBox='0 0 " & Width & " " & Height & "'>" &
        "  <foreignObject width='100%' height='100%'>" &
        "    <div xmlns='http://www.w3.org/1999/xhtml' style='" & VisibilityRule & " " & SafeCss & "'>" &
                SafeHtml &
        "    </div>" &
        "  </foreignObject>" &
        "</svg>"
        
    RETURN SvgOutput
annotation DAXLIB_PackageId = NativeHTML
annotation DAXLIB_PackageVersion = 1.0.0